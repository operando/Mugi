buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

// load Properties
def fabricProp = loadProperties("fabric.properties")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.os.operando.mugi"
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [fabricApiKey: fabricProp.getProperty("fabricApiKey")]
        buildConfigField "String", "TWITTER_KEY", "\"${fabricProp.getProperty("TWITTER_KEY")}\""
        buildConfigField "String", "TWITTER_SECRET", "\"${fabricProp.getProperty("TWITTER_SECRET")}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true;
    }
}

/**
 * Reads properties from the specified .properties file.
 */
def loadProperties(filename) {
    def props = new Properties()
    file(filename).withInputStream {
        props.load(it)
    }
    return props
}
